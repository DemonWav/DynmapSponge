buildscript {
    ext.kotlin_version = "1.0.4"

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.qixalite:SpongeStart:1.5.2"
        classpath "gradle.plugin.org.spongepowered:spongegradle:0.7"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: "kotlin"
apply plugin: "org.spongepowered.plugin"
apply plugin: "com.qixalite.spongestart"
apply plugin: "com.github.johnrengelman.shadow"

group = pluginGroup
version = pluginVersion

repositories {
    mavenCentral()
    maven {
        url "http://repo.mikeprimm.com"
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:5.0.0-SNAPSHOT"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "us.dynmap:DynmapCore:2.4-SNAPSHOT"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

sponge.plugin.id = pluginId

spongestart {
    eula true

    //optional configs
    spongeVanillaBuild "LATEST"
    vanillaServerFolder project.file("run/vanilla").getAbsolutePath()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.demonwav.dynmapsponge.Main'
    }
}

shadowJar {
    baseName = 'DynmapSponge'
    classifier = null
    version = null
}

afterEvaluate {
    build.dependsOn shadowJar

    defaultTasks "build"
}
